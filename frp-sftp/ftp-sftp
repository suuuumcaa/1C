Процедура ЗагрузитьНаСервере()
	// Влаимодействие с пользователем
	Сообщение = Новый СообщениеПользователю;
	
	Попытка		
		// Задаем параметры подключения
		sessionOptions = Новый COMОбъект("WinSCP.SessionOptions"); // Создаем объект SessionOptions
		sessionOptions.HostName = ЭтотОбъект.Хост;
		sessionOptions.UserName = ЭтотОбъект.ИмяПользователя;
		sessionOptions.Password = ЭтотОбъект.Пароль;
		sessionOptions.PortNumber = ЭтотОбъект.Порт;
		sessionOptions.SshHostKeyFingerprint = ЭтотОбъект.ОтпечатокКлючаХостаSsh;
		
		// Создаем объект Session
		session = Новый COMОбъект("WinSCP.Session");
		
		// Параметр необходимо использовать если пути регистрации DLL и исполняемого файла различны
		session.ExecutablePath = ЭтотОбъект.ПутьКWinSCP;
		
		// Подключаемся
		session.Open(sessionOptions);
	Исключение
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	// Скачиваем файлы
	Попытка
		ПутьЗагрузки = ЭтотОбъект.КаталогЗагрузки + "\*";
		ПутьВыгрузкиНаСФТП = ЭтотОбъект.КаталогВыгрузкиНаСФТПСервере + ЭтотОбъект.РасширениеФайла;
		// Загружаем файлы с удаленного сервера
		transferResult = session.GetFiles(ПутьВыгрузкиНаСФТП, ПутьЗагрузки);
		
		// Проверяем возможные ошибки (чаще всего из-за отсутствия прав доступа)
		transferResult.Check();
		
		// Обрабатываем результат
		Для каждого ЗагруженныйФайл Из transferResult.Transfers Цикл
			Сообщение.Текст = "Загружен файл: " + ЗагруженныйФайл.FileName;
			Сообщение.Сообщить();
			
			// Чтение "*.AIR"
			Если ЭтотОбъект.РасширениеФайла = "*.AIR" Тогда
				ИмяФайла = СтрЗаменить(ЗагруженныйФайл.FileName, ЭтотОбъект.КаталогВыгрузкиНаСФТПСервере, "");
				ПутьКЗагруженномуФайлу = ЭтотОбъект.КаталогЗагрузки + "\" + ИмяФайла;
				СоздатьНовыйДокумент(ПутьКЗагруженномуФайлу);
			КонецЕсли;
			
			// Удаляем уже скаченные файлы
			Если ЭтотОбъект.УдалениеФайловНаСервереПослеЗагрузки Тогда
				session.RemoveFiles(ЗагруженныйФайл.FileName);
			КонецЕсли;
		КонецЦикла;
	Исключение
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
	
	// Отключаемся от sftp-сервера
	Попытка
		session.Dispose();
		Сообщение.Текст = "Соединение разорвано";
		Сообщение.Сообщить();
	Исключение
		Сообщение.Текст = "Ошибка отключения соединения: " + ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
КонецПроцедуры